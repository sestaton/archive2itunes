#!perl

use strict;
use warnings;
use autodie;
use File::Basename;
use File::Spec;
use File::Copy qw(move);
use HTTP::Tiny;
use WWW::Mechanize;
use MP3::Tag;
use Getopt::Long;
use Pod::Usage;

our $VERSION = '0.01';

my %opt;
GetOptions(\%opt, 'link|l=s', 'help|h', 'man|m');

## parse args
usage() and exit(0) if $opt{help};
pod2usage( -verbose => 2 ) if $opt{man};
usage() and exit(1) unless $opt{link};

## get tracks and transfer to iTunes
my ( $files, $info ) = fetch_tracks( $opt{link} );
print_info( $info );
transfer_to_itunes( $files );

exit;
#
# methods
#
sub fetch_tracks {
    my ($url) = @_;
    my $mech  = WWW::Mechanize->new();
    $mech->get($url);

    my @links = $mech->links();

    my $list;
    for my $link (@links) {
	if (defined $link->text && $link->text eq 'VBR' && $link->url =~ /\.m3u$/) {
	    #print "Description: ",v$link->text, "\n";
	    #print "URL: ", $link->url, "\n";
	    $list = $link->url();
	}
    }
    
    my $urlbase = 'https://archive.org';
    if (defined $list) {
	$urlbase .= $list;
    }
    else {
	my $show = basename($url);
	$urlbase .= "/download/$show/".$show.'_vbr.m3u';
    }

    my $response = HTTP::Tiny->new->get($urlbase);

    unless ($response->{success}) {
	die "Can't get url $url -- Status: ", $response->{status}, " -- Reason: ", $response->{reason};
    }

    print STDERR "=====> Fetching tracks, please be patient..";

    my (@files, $info);
    for my $track (sort split /\n/, $response->{content}) {
	print STDERR ".";
	my $track_file = fetch_link($track);
	$info = get_track_info($track_file, $info);
	push @files, $track_file;
    }
    print STDERR "Done fetching tracks.\n";

    return (\@files, $info);
}

sub transfer_to_itunes {
    my ($files) = @_;

    my $user = `whoami`;
    chomp $user;
    my $itunes = File::Spec->catdir('/', 'Users', $user, 'Music', 'iTunes');
    my $add_to = File::Spec->catdir($itunes, 'iTunes Music', 'Automatically Add to iTunes');

    for my $file (@$files) {
	my $itunes_file = File::Spec->catfile($add_to, $file);
	move $file, $itunes_file or die $!;
    }
    print STDERR "=====> Complete. Now, fire up iTunes and enjoy!\n";
}

sub print_info {
    my ($info) = @_;

    print STDERR "=====> Transferring the following information to iTunes:\n";
    print STDERR "--------------------------------------------------------\n";

    for my $artist (keys %$info) {
	print STDERR "Artist: $artist\n";
	for my $album (keys %{$info->{$artist}}) {
	    print STDERR "Show:   $album\n";
	    for my $track (sort { $a =~ /^(\d+)/ <=> $b =~ /^(\d+)/ } @{$info->{$artist}{$album}}) {
		my ($num, $desc) = split /\-/, $track;
		print STDERR "Track:  $num - $desc\n";
	    }
	}
    }

    print STDERR "--------------------------------------------------------\n";
}

sub fetch_link {
    my ($link) = @_;

    my $file = basename($link);
    my $response = HTTP::Tiny->new->get($link);

    unless ($response->{success}) {
	die "Can't get url $link -- Status: ", $response->{status}, " -- Reason: ", $response->{reason};
    }
    
    open my $out, '>', $file;
    print $out $response->{content};
    close $out;

    return $file;
}

sub get_track_info {
    my ($file, $info) = @_;
    my ($name, $path, $suffix) = fileparse($file, qr/\.[^.]*/);
    my $mp3 = MP3::Tag->new($file);

    my ($title, $track, $artist, $album, $comment, $year, $genre) = $mp3->autoinfo();
    push @{$info->{$artist}{$album}}, $track."-".$title;

    return $info;
}

sub usage {
    my $prog = basename($0);
    print STDERR <<END

$prog version: $VERSION

USAGE: $prog [-l] [-h] [-m]

Required:
    -l|link     :    The URL to the show to transfer to iTunes.

Options:
    -h|help     :    Print a usage statement.
    -m|man      :    Print the full documentation.

END
}

__END__

=head1 NAME
 
 archive2itunes - Fetch shows from archive.org and add them to your iTunes library from the command line

=head1 SYNOPSIS    

 archive2itunes -l https://archive.org/details/gd67-01-27.aud.hanno.16744.sbeok.shnf

=head1 DESCRIPTION
     
 This tool takes a link to a live show on archive.org and adds the show to your iTunes library.

=head1 DEPENDENCIES

The modules WWW::Mechanize, HTTP::Tiny, and MP3::Tag are required. See below for information on which Perls have been tested.

=head1 LICENSE
 
The MIT License should included with the project. 
If not, it can be found at: L<http://opensource.org/licenses/mit-license.php>

Copyright (C) 2015 S. Evan Staton
 
=head1 TESTED WITH:

=over

=item *
Perl 5.20.1 (Mac OS X 10.6.8)

=back

=head1 AUTHOR 

S. Evan Staton, C<< <statonse at gmail.com> >>

=head1 REQUIRED ARGUMENTS

=over 2

=item -l, --link

  The URL of the live show to retrieve.

=back

=head1 OPTIONS

=over 2

=item -h, --help

  Print a usage statement for a specific command. 

=item -m, --man
  
  Print the full documentation for a specific command.

=back

=cut
